plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    version = '0.0.11-SNAPSHOT'
    sourceCompatibility = '8'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.2.4.RELEASE")
        }
    }

    // common dependencies for all modules
    dependencies {

        // Spring Boot's testing dependencies
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // annotation processor to create configuration metadata
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    javadoc.failOnError = false
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    test {
        useJUnitPlatform {
            if(System.getProperty("ci") != null){
                excludeTags "IgnoreInCi"
            }
        }
        systemProperty "aws.accessKeyId", "foo" // dummy AWS credentials because the AWS SDK complains otherwise
        systemProperty "aws.secretKey", "foo"   // dummy AWS credentials because the AWS SDK complains otherwise
    }

    test.finalizedBy jacocoTestReport

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/CargoAi/sqs-library")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
            publications {
                gpr(MavenPublication) {
                    from(components.java)
                }
            }
        }
    }
}
